use program::posix::ast;

grammar;

pub Program: ast::Program = {
    <p: Program> "\n"+ <c: Commands> => {
        let (mut p, c) = (p, c);
        p.0.push(box c);
        p
    },
    <p: Program> ";" <c: Commands> => {
        let (mut p, c) = (p, c);
        p.0.push(box c);
        p
    },
    <p: Program> "&" <c: Commands> => {
        let (mut p, c) = (p, c);
        p.0.push(box c);
        p
    },
    <p: Program> ";" => p,
    <p: Program> "&" => p,
    Commands => ast::Program(vec![box <>]),
}

Commands: ast::Command = {
    "(" <p: Program> ")" => {
        ast::Command::Subshell(box p)
    },
    // BridgedProgram => <>,
    <cs: Commands> "&&" <p: Pipeline> => {
        ast::Command::And(box cs, box p)
    },
    <cs: Commands> "||" <p: Pipeline> => {
        ast::Command::Or(box cs, box p)
    },
    Pipeline => <>,
}

Pipeline: ast::Command = {
    "!" <ps: PipelineSeq> => ast::Command::Not(box ps),
    <ps: PipelineSeq> => ps,
}

PipelineSeq: ast::Command = {
    <ps: PipelineSeq> "|" "\n"* <c: Command> => {
        ast::Command::Pipeline(box ps, box c)
    },
    <c: Command> => c,
}

pub Command: ast::Command = Word+ => ast::Command::Simple(<>);

pub Word: ast::Word = r"[a-zA-Z0-9-_]+" => ast::Word(<>.into());
